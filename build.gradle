plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'com.progmeth.project'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.clojars.org'
        name 'Clojars'
    }
}

ext {
    junitVersion = '5.9.0'
}

sourceCompatibility = '18'
targetCompatibility = '18'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.progmeth.project.sheriff'
    mainClass = 'com.progmeth.project.sheriff.Main'
}

javafx {
    version = '18.0.1'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.media']
}

javadoc {
    options {
        encoding = 'UTF-8'
        links 'https://docs.oracle.com/en/java/javase/18/docs/api'
        links 'https://openjfx.io/javadoc/18'
    }
}

dependencies {
    implementation 'org.ow2.asm:asm:9.4'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.5'
    implementation('org.controlsfx:controlsfx:11.1.2')
    implementation('org.kordamp.ikonli:ikonli-javafx:12.3.1')
    implementation('com.github.almasb:fxgl:17.1') {
        exclude(group: 'org.openjfx')
    }
    implementation 'com.esotericsoftware:kryonet:2.22.0-RC1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.esotericsoftware.kryo:kryo5:5.3.0'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}

task installLocalGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'scripts/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0775
}

// test

build.dependsOn installLocalGitHook
